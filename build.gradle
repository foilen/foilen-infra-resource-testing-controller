plugins {
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = 11

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
  }
}

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {
  compileOnly "com.foilen:foilen-infra-plugin-core:$foilenInfraPluginVersion"
  compileOnly "com.foilen:foilen-infra-resource-example:$foilenInfraResourceExampleVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask(":uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      pom.project {
        name = "foilen-infra-resource-testing-controller"
        packaging 'jar'
        artifactId 'foilen-infra-resource-testing-controller'
        description 'A plugin with some resources for testing purpose'
        url 'https://github.com/foilen/foilen-infra-resource-testing-controller'

        scm {
          connection = "scm:git:git@github.com:foilen/foilen-infra-resource-testing-controller.git"
          url = "https://github.com/foilen/foilen-infra-resource-testing-controller"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id 'foilen'
            name 'Simon Levesque'
            email 'simon@foilen.com'
          }
        }
      }
    }
  }
}
